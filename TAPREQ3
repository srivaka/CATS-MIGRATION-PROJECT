/*REXX*/                                                                        
ADDRESS "ISPEXEC"                                                               
"CONTROL ERRORS RETURN"                                                         
/*---------------------------------------*/                                     
/* PART 1: INITIALIZATION                */                                     
/*---------------------------------------*/                                     
RETURN_CODE = 0                                                                 
/*                                       */                                     
/* GET GLOBALS                           */                                     
/*                                       */                                     
"VGET (CURRWKDY CURRDD CURRMMM CURRYYYY REQSEL ENVIR WKDY TAPDSN)"              
/*                                       */                                     
/* SET UP TABLE OF MAX DAYS PER MONTH    */                                     
/*                                       */                                     
MAXDAYS = "312831303130313130313031"                                            
DO I = 1 TO 12                                                                  
   PARSE VAR MAXDAYS MAXDAY.I 3 MAXDAYS                                         
END                                                                             
/*                                       */                                     
/* SET UP ALPHA MONTH TABLE              */                                     
/*                                       */                                     
MTHSALPHA = "JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC"                              
DO I = 1 TO 12                                                                  
   MTHALPHA.I = SUBSTR(MTHSALPHA,(I - 1) * 3 + 1,3)                             
END                                                                             
/*                                       */                                     
/* DEFAULT THE CURRENT RUN DATE          */                                     
/*                                       */                                     
DATXDD    = CURRDD                                                              
DATXMMM   = CURRMMM                                                             
DATXYYYY  = CURRYYYY                                                            
/*---------------------------------------*/                                     
/* PART 2: BRING UP THE REQUEST PANEL    */                                     
/*---------------------------------------*/                                     
CRSRFLD = "DATXDD"                                                              
REDISPLAY:                                                                      
"DISPLAY PANEL(TAPPREQ3)"                                                       
IF RC ^= 0 THEN DO                                                              
   "SETMSG MSG(TAPM006)"                                                        
   RETURN_CODE = 1                                                              
   SIGNAL "EXIT"                                                                
END                                                                             
/*----------------------------------------*/                                    
/* PART 3: EDITING NOT DONE IN PANEL      */                                    
/*----------------------------------------*/                                    
/*                                        */                                    
/* DAY OF THE MONTH                       */                                    
/*                                        */                                    
DATXMM = (POS(DATXMMM,MTHSALPHA) - 1) / 3 + 1                                   
IF DATXDD > MAXDAY.DATXMM THEN DO                                               
   "SETMSG MSG(TAPM000)"                                                        
   CRSRFLD = "DATXDD"                                                           
   SIGNAL "REDISPLAY"                                                           
END                                                                             
/*                                        */                                    
/*----------------------------------------*/                                    
/* PART 4: PAD VALUES WHERE REQUIRED      */                                    
/*----------------------------------------*/                                    
DATXDD = RIGHT(DATXDD,2,"0")                                                    
/*                                        */                                    
/*----------------------------------------*/                                    
/* PART 5: STORE VALUES                   */                                    
/*----------------------------------------*/                                    
"VPUT (DATXDD DATXMMM DATXYYYY)"                                                
/*----------------------------------------*/                                    
/* PART 6: RUN EXEC TO UPDATE PARMS       */                                    
/*----------------------------------------*/                                    
ADDRESS TSO "%TAPUPDT"                                                          
EXIT:                                                                           
EXIT RETURN_CODE                                                                
