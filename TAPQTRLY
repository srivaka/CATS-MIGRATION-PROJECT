/*REXX*/                                                                        
ADDRESS "ISPEXEC"                                                               
"CONTROL ERRORS RETURN"                                                         
/*---------------------------------------*/                                     
/* PART 1: INITIALIZATION                */                                     
/*---------------------------------------*/                                     
RETURN_CODE = 0                                                                 
/*                                       */                                     
/* GET GLOBALS                           */                                     
/*                                       */                                     
"VGET (CURRWKDY CURRDD CURRMMM CURRYYYY PANSEL ENVIR WKDY TAPDSN)"              
"VGET (DATXDD DATXMMM DATXYYYY)"                                                
/*                                       */                                     
/* SET UP TABLE OF MAX DAYS PER MONTH    */                                     
/*                                       */                                     
MAXDAYS = "312831303130313130313031"                                            
/*                                       */                                     
/* CAPS-I-L DOES NOT SUPPORT LEAP YEARS  */                                     
DO I = 1 TO 12                                                                  
   PARSE VAR MAXDAYS MAXDAY.I 3 MAXDAYS                                         
END                                                                             
/*                                       */                                     
/* SET UP ALPHA MONTH TABLE              */                                     
/*                                       */                                     
MTHSALPHA = "JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC"                              
DO I = 1 TO 12                                                                  
   MTHALPHA.I = SUBSTR(MTHSALPHA,(I - 1) * 3 + 1,3)                             
END                                                                             
/*                                       */                                     
/* DEFAULT THE QUARTER START DATE        */                                     
/*                                       */                                     
CMTHSUB   = (POS(CURRMMM,MTHSALPHA) - 1) / 3 + 1                                
SQTRDD = 1                                                                      
SUB = (CMTHSUB - 1) % 3 * 3 + 1 /* SUB BECOMES 1 IN 1ST QUARTER, 4 IN */        
SQTRMMM = MTHALPHA.SUB          /* 2ND QTR, 7 IN 3RD, AND 10 IN 4TH.  */        
SQTRYYYY = DATXYYYY                                                             
/*                                       */                                     
/*---------------------------------------*/                                     
/* PART 2: BRING UP THE QUARTERLY PANEL  */                                     
/*---------------------------------------*/                                     
CRSRFLD = "SQTRDD"                                                              
REDISPLAY:                                                                      
"DISPLAY PANEL(TAPPQTR)"                                                        
IF RC ^= 0 THEN DO                                                              
   "SETMSG MSG(TAPM006)"                                                        
   RETURN_CODE = 1                                                              
   SIGNAL "EXIT"                                                                
END                                                                             
/*----------------------------------------*/                                    
/* PART 3: EDITING NOT DONE IN PANEL      */                                    
/*----------------------------------------*/                                    
/*                                        */                                    
/* DAY OF THE MONTH                       */                                    
/*                                        */                                    
DATXMM = (POS(DATXMMM,MTHSALPHA) - 1) / 3 + 1                                   
IF DATXDD > MAXDAY.DATXMM THEN DO                                               
   "SETMSG MSG(TAPM000)"                                                        
   CRSRFLD = "DATXDD"                                                           
   SIGNAL "REDISPLAY"                                                           
END                                                                             
/*                                        */                                    
SQTRMM = (POS(SQTRMMM,MTHSALPHA) - 1) / 3 + 1                                   
IF SQTRDD > MAXDAY.SQTRMM THEN DO                                               
   "SETMSG MSG(TAPM000)"                                                        
   CRSRFLD = "SQTRDD"                                                           
   SIGNAL "REDISPLAY"                                                           
END                                                                             
/*                                        */                                    
/*----------------------------------------*/                                    
/* PART 4: PAD VALUES WHERE REQUIRED      */                                    
/*----------------------------------------*/                                    
DATXDD = RIGHT(DATXDD,2,"0")                                                    
SQTRDD = RIGHT(SQTRDD,2,"0")                                                    
/*                                        */                                    
/*----------------------------------------*/                                    
/* PART 5: STORE VALUES                   */                                    
/*----------------------------------------*/                                    
"VPUT (DATXDD DATXMMM DATXYYYY SQTRDD SQTRMMM SQTRYYYY)"                        
EXIT:                                                                           
EXIT RETURN_CODE                                                                
