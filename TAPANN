/*REXX*/                                                                        
ADDRESS "ISPEXEC"                                                               
"CONTROL ERRORS RETURN"                                                         
/*---------------------------------------*/                                     
/* PART 1: INITIALIZATION                */                                     
/*---------------------------------------*/                                     
RETURN_CODE = 0                                                                 
/*                                       */                                     
/* GET GLOBALS                           */                                     
/*                                       */                                     
"VGET (CURRWKDY CURRDD CURRMMM CURRYYYY PANSEL ENVIR WKDY TAPDSN)"              
"VGET (DATXDD DATXMMM DATXYYYY)"                                                
/*                                       */                                     
/* SET UP TABLE OF MAX DAYS PER MONTH    */                                     
/*                                       */                                     
MAXDAYS = "312831303130313130313031"                                            
/*                                       */                                     
DO I = 1 TO 12                                                                  
   PARSE VAR MAXDAYS MAXDAY.I 3 MAXDAYS                                         
END                                                                             
/*                                       */                                     
/* SET UP ALPHA MONTH TABLE              */                                     
/*                                       */                                     
MTHSALPHA = "JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC"                              
DO I = 1 TO 12                                                                  
   MTHALPHA.I = SUBSTR(MTHSALPHA,(I - 1) * 3 + 1,3)                             
END                                                                             
/*                                       */                                     
/* DEFAULT THE NEXT MONTH                */                                     
/*                                       */                                     
CMTHSUB   = (POS(CURRMMM,MTHSALPHA) - 1) / 3 + 1                                
NMTHMM = CMTHSUB + 1                                                            
IF NMTHMM > 12 THEN NMTHMM = 1                                                  
/*                                       */                                     
/* DEFAULT THE NEXT YEAR                 */                                     
/*                                       */                                     
NYERYYYY = DATXYYYY + 1                                                         
/* IF NYERYYYY > 99 THEN NYERYYYY = NYERYYYY - 100 */  /* Y2K */                
/*                                       */                                     
/* DEFAULT THE START OF NEXT YEAR        */                                     
/*                                       */                                     
SNYRDD = 1                                                                      
SNYRMMM = "JAN"                                                                 
SNYRYYYY = DATXYYYY + 1                                                         
/*                                       */                                     
/*---------------------------------------*/                                     
/* PART 2: BRING UP THE ANNUAL PANEL     */                                     
/*---------------------------------------*/                                     
CRSRFLD = "NMTHMM"                                                              
REDISPLAY:                                                                      
"DISPLAY PANEL(TAPPANN)"                                                        
IF RC ^= 0 THEN DO                                                              
   "SETMSG MSG(TAPM006)"                                                        
   RETURN_CODE = 1                                                              
   SIGNAL "EXIT"                                                                
END                                                                             
/*----------------------------------------*/                                    
/* PART 3: EDITING NOT DONE IN PANEL      */                                    
/*----------------------------------------*/                                    
/*                                        */                                    
/* DAY OF THE MONTH                       */                                    
/*                                        */                                    
DATXMM = (POS(DATXMMM,MTHSALPHA) - 1) / 3 + 1                                   
IF DATXDD > MAXDAY.DATXMM THEN DO                                               
   "SETMSG MSG(TAPM000)"                                                        
   CRSRFLD = "DATXDD"                                                           
   SIGNAL "REDISPLAY"                                                           
END                                                                             
/*                                        */                                    
SNYRMM = (POS(SNYRMMM,MTHSALPHA) - 1) / 3 + 1                                   
IF SNYRDD > MAXDAY.SNYRMM THEN DO                                               
   "SETMSG MSG(TAPM000)"                                                        
   CRSRFLD = "SNYRDD"                                                           
   SIGNAL "REDISPLAY"                                                           
END                                                                             
/*                                        */                                    
/*----------------------------------------*/                                    
/* PART 4: PAD VALUES WHERE REQUIRED      */                                    
/*----------------------------------------*/                                    
DATXDD = RIGHT(DATXDD,2,"0")                                                    
NMTHMM = RIGHT(NMTHMM,2,"0")                                                    
NYERYYYY = RIGHT(NYERYYYY,4,"0")                                                
SNYRDD = RIGHT(SNYRDD,2,"0")                                                    
/*                                        */                                    
/*----------------------------------------*/                                    
/* PART 5: STORE VALUES                   */                                    
/*----------------------------------------*/                                    
"VPUT (DATXDD DATXMMM DATXYYYY NMTHMM NYERYYYY SNYRDD SNYRMMM SNYRYYYY)"        
EXIT:                                                                           
EXIT RETURN_CODE                                                                
